BASE <http://example.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
 ?n_name
 (sum(?l_extendedprice * (1 - ?l_discount)) as ?revenue)
WHERE {
  {
    ?customer <c_nation>        ?nation .
    ?order    <o_cust>          ?customer ;
              <o_orderdate>     ?o_orderdate .
    ?lineitem <l_order>         ?order ;
              <l_supp>          ?supplier ;
              <l_extendedprice> ?l_extendedprice ;
              <l_discount>      ?l_discount .
    ?supplier <s_nation>        ?nation .
    ?nation   <n_name>          ?n_name ;
              <n_region>        ?region .
    ?region   <r_name>          "AMERICA" .
  }
  FILTER (strdt(concat(str(?o_orderdate), 'T00:00:00'), xsd:dateTime) >= "1994-01-01T00:00:00"^^xsd:dateTime &&
          strdt(concat(str(?o_orderdate), 'T00:00:00'), xsd:dateTime) <  "1995-01-01T00:00:00"^^xsd:dateTime)
}
GROUP BY ?n_name
