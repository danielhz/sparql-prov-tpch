BASE <http://example.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
  ?order
  (sum(?l_extendedprice * (1 - ?l_discount)) AS ?revenue)
  ?o_orderdate
  ?o_shippriority
WHERE {
   {
     ?customer
       <c_mktsegment> 'BUILDING' .
     ?order
       <o_cust> ?customer ;
       <o_orderdate> ?o_orderdate ;
       <o_shippriority> ?o_shippriority .
     ?lineitem
       <l_order> ?order ;
       <l_shipdate> ?l_shipdate ;
       <l_extendedprice> ?l_extendedprice ;
       <l_discount> ?l_discount .
   }
   FILTER (strdt(concat(str(?o_orderdate), 'T00:00:00'), xsd:dateTime) < "1995-03-23T00:00:00"^^xsd:dateTime &&
           strdt(concat(str(?l_shipdate), 'T00:00:00'), xsd:dateTime) > "1995-03-23T00:00:00"^^xsd:dateTime)
}
GROUP BY ?order ?o_orderdate ?o_shippriority
