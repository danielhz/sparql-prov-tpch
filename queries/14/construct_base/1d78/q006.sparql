BASE <http://example.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
CONSTRUCT {
  ?row <a1> ?promo_revenue .
}
WHERE {
  {
    SELECT
      (((100.00 * sum(if(strstarts(?p_type, "PROMO"), ?l_extendedprice * (1 - ?l_discount), 0)))
        / sum(?l_extendedprice * (1 - ?l_discount))) AS ?promo_revenue)
    WHERE {
      ?lineitem <l_part>          ?part ;
                <l_extendedprice> ?l_extendedprice ;
                <l_discount>      ?l_discount ;
                <l_shipdate>      ?l_shipdate .
      ?part     <p_type>          ?p_type .
      FILTER (strdt(concat(str(?l_shipdate), 'T00:00:00'), xsd:dateTime) >= "1994-08-01T00:00:00"^^xsd:dateTime &&
              strdt(concat(str(?l_shipdate), 'T00:00:00'), xsd:dateTime) < "1994-09-01T00:00:00"^^xsd:dateTime)
    }
  }
  BIND (URI("http://example.org/row/") AS ?row)
}
