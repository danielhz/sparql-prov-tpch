BASE <http://example.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
  (((100.00 * sum(if(strstarts(?p_type, "PROMO"), ?l_extendedprice * (1 - ?l_discount), 0)))
    / sum(?l_extendedprice * (1 - ?l_discount))) AS ?promo_revenue)
WHERE {
  ?lineitem <l_part>          ?part ;
            <l_extendedprice> ?l_extendedprice ;
            <l_discount>      ?l_discount ;
            <l_shipdate>      ?l_shipdate .
  ?part     <p_type>          ?p_type .
  FILTER (?l_shipdate >= "1993-10-01T00:00:00"^^xsd:dateTime &&
          ?l_shipdate < "1993-11-01T00:00:00"^^xsd:dateTime)
}
