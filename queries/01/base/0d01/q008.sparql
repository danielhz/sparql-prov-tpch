BASE <http://example.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT
  ?l_returnflag
  ?l_linestatus
  (sum(?l_quantity) as ?sum_qty)
  (sum(?l_extendedprice) as ?sum_base_price)
  (sum(?l_extendedprice * (1 - ?l_discount)) as ?sum_disc_price)
  (sum(?l_extendedprice * (1 - ?l_discount) * (1 + ?l_tax)) as ?sum_charge)
  (avg(?l_quantity) as ?avg_qty)
  (avg(?l_extendedprice) as ?avg_price)
  (avg(?l_discount) as ?avg_disc)
  (count(*) as ?count_order)
WHERE {
  ?lineitem
    <l_quantity> ?l_quantity ;
    <l_tax> ?l_tax ;
    <l_shipdate> ?l_shipdate ;
    <l_extendedprice> ?l_extendedprice ;
    <l_discount> ?l_discount ;
    <l_returnflag> ?l_returnflag ;
    <l_linestatus> ?l_linestatus .
  FILTER (strdt(concat(str(?l_shipdate), 'T00:00:00'), xsd:dateTime) <= "1998-09-29T00:00:00"^^xsd:dateTime)
}
GROUP BY ?l_returnflag ?l_linestatus
